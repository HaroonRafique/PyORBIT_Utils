/******************************************************************************
 * INITIALIZATION
 ******************************************************************************/
RFV = 0.024;


TITLE, "Injection optics for space charge studies";

call,  file = './PS/PS_new.ele';
call,  file = './PS/PS_new.seq';
call,  file = './PS/PS_new.str';
call,  file = './PS/PS_2013.aper';
/******************************************************************************
 * BEAM and USE
 ******************************************************************************/

beam, particle=proton, pc=pc;
use, sequence=PS;
  
PR.BWSH65     :  MONITOR  , L = 0.0; 

 seqedit, sequence=PS;
  flatten;
  install, element=PR.BWSH65, at=0.0, from=PS65$START;
  cycle , start=PR.BWSH65;
  flatten;
  endedit;
save,file='PS/PS.seq';


  use, sequence=PS;


value, pc, beam->pc, beam->energy;

/******************************************************************
 * MATCHING
 ******************************************************************/
ptc_twiss_tune_macro_false_split: macro={
  ptc_create_universe;
  ptc_create_layout, time=true, model=2, exact=true, method=6, nst=3;
  !ptc_script, file="resplit.ptc";
  ptc_twiss, closed_orbit, table=ptc_twiss,icase=56,no=2,summary_table=ptc_twiss_summary;
  qx0=table(ptc_twiss_summary,Q1);
  qy0=table(ptc_twiss_summary,Q2);
  value, qx0, qy0;
  ptc_end;
};

use, sequence=PS;
match, use_macro;
  vary,   name=iqf, step=1.0E-6 ;
  vary,   name=iqd, step=1.0E-6 ;
  USE_MACRO, name=ptc_twiss_tune_macro_false_split;
  CONSTRAINT, expr=  table(ptc_twiss_summary,Q1)= 0.19;
  CONSTRAINT, expr=  table(ptc_twiss_summary,Q2)= 0.24;
  JACOBIAN,calls=10000,bisec=3,TOLERANCE=1.0E-21;
ENDMATCH;
value, IQF, IQD;


/******************************************************************
 * Generate flat file
 ******************************************************************/

ptc_create_universe;
ptc_create_layout,time=true, model=2, exact=true, method=6, nst=3;
ptc_script, file="./PS/resplit.ptc";
ptc_script, file="./print_flat_file.ptc"; 
ptc_end;



ptc_create_universe;
ptc_create_layout,time=true, model=2, exact=true, method=6, nst=3;
select, flag=ptc_twiss, clear;
select, flag=ptc_twiss, column=name,  betx, px, bety, py, disp3, disp3p, disp1, disp1p;
ptc_twiss, icase=5, no=4, deltap_dependency, closed_orbit, file, table=ptc_twiss;
ptc_end;


